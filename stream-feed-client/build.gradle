import io.getstream.feed.Configuration
import io.getstream.feed.Dependencies

import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'kotlin'
    id 'kotlin-kapt'
}

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'stream-feed-client'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

tasks.named('jar') {
    manifest {
        attributes["Implementation-Build-Date"] = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        attributes["Implementation-Title"] = "stream-feed-client"
        attributes["Implementation-Vendor"] = "GetStream"
        attributes["Implementation-Vendor-Id"] = Configuration.artifactGroup
        attributes["Implementation-Version"] = rootVersionName
        attributes["Created-By"] = "Gradle ${gradle.gradleVersion}"
        attributes["Build-Jdk"] = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        attributes["Build-OS"] = "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    }
}

dependencies {
    implementation Dependencies.coroutinesCore
    implementation Dependencies.moshi
    implementation Dependencies.moshiAdapters
    implementation Dependencies.arrow

    kapt Dependencies.moshiCodegen

    implementation Dependencies.retrofitMoshiConverter
    implementation Dependencies.retrofit
    implementation Dependencies.okhttpLoggingInterceptor
    implementation Dependencies.ok2curl

    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testImplementation Dependencies.jsonAssert

    testRuntimeOnly Dependencies.junitJupiterEngine

    testImplementation Dependencies.kluent
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.coroutinesTest
}